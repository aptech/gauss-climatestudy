// This program file loads and graphs the NASA data
dir = "data/globaltemps/data/";
datafile_temps = "globaltempindex.xlsx";
datafile_sealevel = "globalsealevel.xlsx";
datafile_co2 = "co2.xlsx";
datafile_icesheets = "icesheets.xlsx";
datafile_arctic = "articseaice.xlsx";

// Load data
temp = loadd(dir $+ datafile_temps);

//Plot global temperature data
startdt = 1880;
freq = 1;

//Time series plot
//Set up graph look
struct plotControl tsPlot;
tsPlot = projPlotDefaults();

// Set ticks
plotSetXTicInterval(&tsPlot, 10, 1890);

// Set title
plotSetTitle(&tsPlot, "Change in the NASA Global Land-Ocean Temperature Index (base 1951-1980)");

// Set axis
plotSetXAxisLabel(&tsPlot, "Years");
plotSetYAxisLabel(&tsPlot, "Temperature Anomaly (C)");

// Set Legend
plotSetLegend(&tsPlot, "Smoothed" $| "Annual Mean");

// Time series plot of smoothed data
plotTS(tsPlot, startdt, freq, temp[., 3]);

// Scatter plot of raw data
plotAddScatter(seqa(1, 1, rows(temp)), temp[.,2]);

// Add annotation
// Add the procedure below to your user library
// and you will only need one line for all the settings
x_start = 90;
y_start = -0.5;
plotAddTextbox(grayTextSettings(), "Data Source: NASA's Goddard Institute for Space Studies (GISS)", x_start, y_start);

// Set the line color
plotSetLineColor(&tsPlot, "dark grey");
plotSetLineStyle(&tsPlot, 2);
plotAddXY(tsPlot, 1|rows(temp[.,3]), 0|0);

/*****************************************************************************/


proc (1) = projPlotDefaults();
    local title_font_size, font_name, axis_label_size;
    // Set up graph
    struct plotControl myPlot;
    myPlot = plotGetDefaults("xy");
    
    // Set up font
    title_font_size = 18;
    font_name = "helvetica neue";
    axis_label_size = 14;
    
    
    // Title
    plotSetTitle(&myPlot, "", font_name, title_font_size);
    
    plotSetYLabel(&myPlot, "", font_name, axis_label_size);
    
    // Set legend background to be completely transparent
    plotSetLegendBkd(&myPlot, 0);
    
    plotSetTicLabelFont(&myPlot, font_name, 12);
    plotSetLegendFont(&myPlot, font_name, 12);
    
    retp(myPlot);
endp;


proc (1) = grayTextSettings();
    local font_size, font_name;
    struct plotAnnotation mytextbox;

    // Set up font
    font_size = 10;
    font_name = "helvetica neue";
    
    mytextbox = annotationGetDefaults();
    annotationSetBkd(&mytextbox, "#DDDDDD", 0.1);
    annotationSetFont(&mytextbox, font_name, font_size, "#55555");
    
    annotationSetLineThickness(&mytextbox, 0);
    //annotationSetLineColor(&mytextbox, "#555555");
    
    retp(mytextbox);
endp;
